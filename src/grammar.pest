WHITESPACE = _{ "\t" | "\r" | SPACE_SEPARATOR }
COMMENT = _{
  "/*" ~ (!"*/" ~ ANY)* ~ "*/"
}

DOT = _{ "." }
LAMBDA = _{ "Î»" | "\\" }
VARIABLE_CHAR = _{ LETTER | NUMBER | "_" | "'" }

OPEN_PAREN = _{ "(" }
CLOSE_PAREN = _{ ")" }

main = {
  SOI ~ body
}

body = _{
  line_comment ~ body
  | line ~ ";" ~ body
  | line ~ "\n"*
  | "\n"+ ~ body
  | EOI
}

line = _{
  macro_
  | term
}

line_comment = _{
  "//" ~ (!"\n" ~ ANY)* ~ "\n"
}

// Macros are shortand for expressions that can be reused. Variables in a term
// that share the name of a macro will be replaced by the right-hand side of
// the macro.

macro_ = {
  variable ~ "=" ~ term
}

// This is essentially
//
//   term = ( term ) | variable | abstraction | term term
//
// with the left recursion removed.

variable = @{
  VARIABLE_CHAR+
}

abstraction = !{
  LAMBDA ~ variable ~ DOT ~ term
}

application = !{
  simple_term ~ simple_term+
}

term = !{
  application
  | variable
  | abstraction
  | OPEN_PAREN ~ term ~ CLOSE_PAREN
}

simple_term = !{
  variable
  | abstraction
  | OPEN_PAREN ~ term ~ CLOSE_PAREN
}
