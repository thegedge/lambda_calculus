WHITESPACE = _{ "\n" | "\t" | "\r" | SEPARATOR }
COMMENT = _{
  "/*" ~ (!"*/" ~ ANY)* ~ "*/"
  | "//" ~ ANY+ ~ "\n"
}

DOT = _{ "." }
LAMBDA = _{ "Î»" | "\\" }
VARIABLE_CHAR = _{ LETTER | NUMBER | "_" | "'" }

OPEN_PAREN = _{ "(" }
CLOSE_PAREN = _{ ")" }

MACRO_START = _{ "=" }
MACRO_END = _{ ";" }

main = {
  SOI ~ macros ~ term ~ EOI
}

// Macros are shortand for expressions that can be reused. Variables in a term
// that share the name of a macro will be replaced by the right-hand side of
// the macro.

macros = {
  macro_*
}

macro_ = {
  variable ~ MACRO_START ~ term ~ MACRO_END
}

// This is essentially
//
//   term = ( term ) | variable | abstraction | term term
//
// with the left recursion removed.

variable = @{
  VARIABLE_CHAR+
}

abstraction = !{
  LAMBDA ~ variable ~ DOT ~ term
}

application = !{
  simple_term+
}

term = !{
  application
  | variable
  | abstraction
  | OPEN_PAREN ~ term ~ CLOSE_PAREN
}

simple_term = !{
  variable
  | abstraction
  | OPEN_PAREN ~ term ~ CLOSE_PAREN
}
